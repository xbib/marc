
apply plugin: 'io.github.gradle-nexus.publish-plugin'

publishing {
    publications {
        javaPublication(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = rootProject.ext.description
                url = rootProject.ext.url
                inceptionYear = rootProject.ext.inceptionYear
                packaging = 'jar'
                organization {
                    name = 'xbib'
                    url = 'https://xbib.org'
                }
                developers {
                    developer {
                        id = 'jprante'
                        name = 'JÃ¶rg Prante'
                        email = 'joergprante@gmail.com'
                        url = 'https://github.com/xbib'
                    }
                }
                scm {
                    url = rootProject.ext.scmUrl
                    connection = rootProject.ext.scmConnection
                    developerConnection = rootProject.ext.scmDeveloperConnection
                }
                issueManagement {
                    system = rootProject.ext.issueManagementSystem
                    url = rootProject.ext.issueManagementUrl
                }
                licenses {
                    license {
                        name = rootProject.ext.licenseName
                        url = rootProject.ext.licenseUrl
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
    signing {
        sign publishing.publications.javaPublication
    }
}

if (project.hasProperty("ossrhUsername")) {
    nexusPublishing {
        repositories {
            sonatype {
                username = project.property('ossrhUsername')
                password = project.property('ossrhPassword')
                packageGroup = 'org.xbib'
            }
        }
    }
}
